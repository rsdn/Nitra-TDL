using Tdl;
using Tdl.ScenarioAction;

using DotNet;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Imperative;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Tdl2Json
{
  class BarrierCompiler
  {
    static         _baseStableGuid : Guid = Guid.Parse("{BBBBBBBB-BBBB-BBBB-BBBB-BBBBBBBBBBBB}");
    static mutable _counter        : uint;

    _idTable      : Dictionary[object, Guid] = Dictionary();
    _barrierCount : Dictionary[Guid, int]    = Dictionary();

    public GetMultiMachineId(key : object) : Guid?
    {
      if (key == null)
        null
      else
      {
        mutable id;
        unless (_idTable.TryGetValue(key, out id))
        {
          _counter++;
          id = Api.IncrementGuid(_baseStableGuid, _counter);
          _idTable.Add(key, id);
        }
        id
      }
    }

    public GetMultiMachineId(expr : Tdl.Expr, variables : IReadOnlyDictionary[TypedDeclarationSymbol, Tdl.Expr]) : Guid?
    {
      def getKey(expr : Tdl.Expr) : object
      {
        | NotSet            => null
        | Error             => null
        | String(value)     => value
        | True              => true
        | False             => false
        | Real(value)       => value
        | Integer(value)    => value
        | Reference(symbol) =>
          when (symbol is TypedDeclarationSymbol as typedSymbol)
          {
            mutable substitutedExpr;
            when (variables : object != null && variables.TryGetValue(typedSymbol, out substitutedExpr))
              return getKey(substitutedExpr);
          }
          symbol

        | Tuple(elems, _)
        | List(elems, _) =>
          mutable acc = [expr.GetType() : object];
          for (mutable i = elems.Length - 1; i >= 0; --i)
            acc ::= getKey(elems[i]);
          acc

        | _ => throw NotSupportedException($"$expr ($(expr.GetType().Name))")
      }

      GetMultiMachineId(getKey(expr))
    }

    public UseBarrier(multiMachineId : Guid, name : string) : Guid
    {
      def key       = (multiMachineId, name);
      def barrierId = GetMultiMachineId(key).GetValueOrDefault();

      mutable count;
      unless (_barrierCount.TryGetValue(barrierId, out count))
        count = 0;

      _barrierCount[barrierId] = count + 1;
      barrierId
    }

    public UseBarrier(multiMachineId : Guid, dic : IReadOnlyDictionary[TypedDeclarationSymbol, Tdl.Expr], bg : BarrierGenerator) : Guid
    {
      def name = Text.StringBuilder();
      foreach (part in bg.BarrierName)
      {
        | BarrierNamePart.String as s => _ = name.Append(s.text);
        | BarrierNamePart.Reference as r =>
          if (r.symbol is TypedDeclarationSymbol as tds)
            _ = name.Append(Api.TryResolveVar(dic, tds).Name);
          else
            _ = name.Append(r.symbol.Name);
        | _ => ()
      }

      UseBarrier(multiMachineId, name.ToString())
    }

    public GetBarrierCount(barrierId : Guid) : int
    {
      mutable count;
      def _barrierFound = _barrierCount.TryGetValue(barrierId, out count);
      assert2(_barrierFound);
      count
    }
  } // class
} //namespace
