using DotNet;
using DotNet.AstUtils;

using Nitra;
using Nitra.Declarations;

using System.Collections.Immutable;

namespace Tdl
{
  ast Case : BindableAst
  {
    in ContainingTable : TableScope;
    out Symbols        : ImmutableArray[DeploymentSymbol];
    out Cases          : ImmutableArray[string];

    Deployments.Scope           = Scope;
    Deployments.ContainingTable = ContainingTable;
    Symbols                     = TdlUtils.MakeDeployments(Deployments.Ref);
    Cases                       = Values.Text;

    SetUsed(Symbols, true);

    Values      : CaseValue*;
    Deployments : DeploymentExpr*;
  }

  abstract ast CaseValue : BindableAst
  {
    out Text : string;

    | String
      {
        Text = TdlUtils.ConvertStringLiteralOrNull(Value);

        Value : CSharp.StringLiteral;
      }

    | Wildcard
      {
        Error(context, <#Syntax "_" is no longer supported. Use the "not-set" keyword instead.#>);

        Text = null;
      }

    | NotSet
      {
        Text = null;
      }

    | Else
      {
        Text = Api.CaseElseValue;
      }
  }
}
