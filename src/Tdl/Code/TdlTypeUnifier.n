using Nitra.Declarations;

using DotNet;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Imperative;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Tdl
{
  public class TdlTypeUnifier : DotNet.TypeUnifier
  {
    public new static InitInstance() : void
    {
      Instance = TdlTypeUnifier();
    }

    protected override UnifyUnwraped(typeSymbol1 : TypeSymbol, typeSymbol2 : TypeSymbol, updatedVariables : ref LightList[TypeVarSymbol]) : bool
    {
      match (typeSymbol1, typeSymbol2)
      {
        | (TdlType.TupleSymbol as f1, TdlType.TupleSymbol as f2) =>
          if (f1.IsArgsEvaluated && !f2.IsArgsEvaluated)
            f2.Args = f1.Args;
          else if (!f1.IsArgsEvaluated && f2.IsArgsEvaluated)
            f1.Args = f2.Args;
          else if (!f1.IsArgsEvaluated && !f2.IsArgsEvaluated)
          {
            //assert2(false, "FunctionTypeSymbol Unify failed!");
            //return false;
          }
          else unless (RequireAll(f1.Args, f2.Args, ref updatedVariables))
            return false;

          true

        | _ =>
          when (typeSymbol1 : object is ConstructedTypeSymbol(TypeInfo=ti1, Args=args1))
          when (ti1 : object is SupportsInheritanceTypeSymbol(IsBaseTypeSetEvaluated=true, BaseTypeSet=baseTypeSet))
          {
            when (baseTypeSet.ContainsAncestor(typeSymbol2))
              return true;
            when (typeSymbol2 is TypeVarSymbol(IsBaseTypeConstraintEvaluated=true, BaseTypeConstraint=ConstructedTypeSymbol(TypeInfo=ti2, Args=args2)) as tv2)
            when (baseTypeSet.ContainsAncestor(ti2))
            {
              unless(UnifyAll(args1, args2, ref updatedVariables))
              {
                return false;
              }
              unless (tv2.IsDerivedTypeConstraintEvaluated)
              {
                tv2.DerivedTypeConstraint = typeSymbol1;
              }
              return true;
            }
          }
          base.UnifyUnwraped(typeSymbol1, typeSymbol2, ref updatedVariables)
      }
    }

    public TryUnifyAll(ts1 : IList[TypeSymbol], ts2 : IList[TypeSymbol]) : bool
    {
      mutable updatedVariables;
      def result = UnifyAll(ts1, ts2, ref updatedVariables);
      ResetTypeVars(ref updatedVariables);
      result
    }
    
    protected override SubtypeUnifyUnwraped(subtype : TypeSymbol, supertype : TypeSymbol, updatedVariables : ref LightList[TypeVarSymbol]) : bool
    {
      when (subtype is TypeVarSymbol(IsTypeEvaluated=false, IsDerivedTypeConstraintEvaluated=true) as sub)
      {
        unless (SubtypeUnifyUnwraped(sub.DerivedTypeConstraint, supertype, ref updatedVariables))
          return false;

        return 
          if (sub.IsBaseTypeConstraintEvaluated)
            SubtypeUnifyUnwraped(supertype, sub.BaseTypeConstraint, ref updatedVariables)
          else
          {
            sub.BaseTypeConstraint = supertype;
            true
          };
      }

      when (subtype.Equals(supertype))
        return true;

      when (supertype is TopConstructedTypeSymbol(IsTypeInfoEvaluated=true, IsArgsEvaluated=true, TypeInfo=supertypeInfo, Args=supertypeArgs))
      {
        match (subtype)
        {
          | SupportsInheritanceTypeSymbol(IsBaseTypeSetEvaluated=true, BaseTypeSet=baseTypeSet) =>
            foreach (TopConstructedTypeSymbol(TypeInfo=ancestorInfo, Args=ancestorArgs) in baseTypeSet.AncestorTypes)
            {
              when (supertypeInfo.Equals(ancestorInfo) && TryUnifyAll(supertypeArgs, ancestorArgs))
                return UnifyAll(supertypeArgs, ancestorArgs, ref updatedVariables);
            }

          | TopConstructedTypeSymbol(IsTypeInfoEvaluated=true, IsArgsEvaluated=true, TypeInfo=subtypeInfo, Args=typeParameterValues) =>
            when ((subtypeInfo : object) is SupportsInheritanceTypeSymbol(IsBaseTypeSetEvaluated=true, BaseTypeSet=baseTypeSet))
            {
              foreach (TopConstructedTypeSymbol(TypeInfo=ancestorInfo, Args=ancestorArgs) in baseTypeSet.AncestorTypes)
              {
                when (supertypeInfo.Equals(ancestorInfo))
                {
                  mutable substitutedValues;
                  _ = SymbolUtils.TrySubstituteTypeParameters(ancestorArgs, typeParameterValues, out substitutedValues);

                  when (TryUnifyAll(supertypeArgs, substitutedValues))
                    return UnifyAll(supertypeArgs, substitutedValues, ref updatedVariables);
                }
              }
            }

          | _ => ()
        }
      }

      match (subtype)
      {
        | TopConstructedTypeSymbol(IsTypeInfoEvaluated=true, TypeInfo=subtypeInfo) => SubtypeUnifyUnwraped(subtypeInfo, supertype, ref updatedVariables)
        | _ when TryUnify(subtype, supertype) => UnifyUnwraped(subtype, supertype, ref updatedVariables)
        | _ => base.SubtypeUnifyUnwraped(subtype, supertype, ref updatedVariables)
      }
    }
  } // class
} // namespace
